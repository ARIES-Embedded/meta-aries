#@TYPE: Machine
#@NAME: DENX MCV SoM Evaluation Kit
#@SOC:  Altera Cyclone V SoC
#@DESCRIPTION: Machine configuration for DENX MCV SoM Evaluation Kit

DEFAULTTUNE ?= "cortexa9thf-neon"
SOC_FAMILY ?= "socfpga"

require conf/machine/include/arm/armv7a/tune-cortexa9.inc
require conf/machine/include/soc-family.inc

MACHINE_FEATURES ?= "apm ext2 keyboard screen serial touchscreen usbgadget usbhost pci"

SERIAL_CONSOLE = "115200 ttyS0"

EXTRA_IMAGEDEPENDS += "u-boot"

# U-Boot bootloader
UBOOT_ENTRYPOINT := "0x00008000"
UBOOT_MAKE_TARGET := "u-boot-with-spl.sfp"
UBOOT_BINARY := "u-boot-with-spl.sfp"
UBOOT_SUFFIX ?= "sfp"
UBOOT_MACHINE = "socfpga_mcvevk_defconfig"

PREFERRED_VERSION_u-boot ?= "2022.01%"
PREFERRED_VERSION_u-boot-tools ?= "2022.01%"
PREFERRED_PROVIDER_u-boot-fw-utils ?= "libubootenv"

# Linux kernel
KERNEL_IMAGETYPE = "fitImage"
KERNEL_CLASSES += " kernel-fitimage "
KERNEL_DEVICETREE = "socfpga_cyclone5_mcvevk.dtb socfpga_cyclone5_mcvevp_rev2_fb.dtb"

#PREFERRED_PROVIDER_virtual/kernel ?= "linux-altera-lts"
#PREFERRED_VERSION_linux-altera-lts ?= "5.10%"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-altera-lts"
PREFERRED_VERSION_linux-altera-lts ?= "5.15%"

# Ship kernel modules
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "kernel-modules kernel-devicetree"

# List of images to be built
IMAGE_FSTYPES ?= "tar.xz ext4 wic.xz"
INITRAMFS_FSTYPES ?= "cpio.xz"

WKS_FILE = "sdimage-mcvevk.wks"
IMAGE_BOOT_FILES ?= "${KERNEL_IMAGETYPE}"
IMAGE_INSTALL:append = " kernel-devicetree kernel-image-fitimage"
do_image_wic[depends] += "mtools-native:do_populate_sysroot dosfstools-native:do_populate_sysroot"
